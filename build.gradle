buildscript {
    ext.app_version = "1.0.0"

    ext.kotlin_version = '1.3.11'
    ext.spring_boot_version = '2.0.4.RELEASE'

    ext['netty.version'] = '4.1.29.Final'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //spring
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        // Required for Kotlin integration for spring
        // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        //fixes no-arg constructor errors
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
    }
}

plugins {
//db migrations
    id "org.flywaydb.flyway" version "5.1.4"
}

group 'ru.kuchanov.scp'
version "$app_version"

apply plugin: 'java'
apply plugin: 'kotlin'
//See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: "kotlin-spring"
//use entities with data classes
apply plugin: "kotlin-jpa"

//spring boot
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//artifacts
bootWar {
    version ""
    baseName = 'scp-reader'
}

//to be able to run with task args to define correct properties file
//i.e. `bootRun -Dspring.profiles.active=dev`
bootRun.systemProperties = System.properties as Map<String, ?>

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    //need as spring version of netty conflicts with one in firebase libs
    implementation 'io.netty:netty-all:4.1.29.Final'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    //fix classNotFound
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //spring
    implementation "org.springframework.boot:spring-boot-starter-web"
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    //info
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    //data
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    //security
    implementation "org.springframework.boot:spring-boot-starter-security"
    //oauth2
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.3.0.RELEASE'
    //spring END

    //postgresql
    implementation 'org.postgresql:postgresql:42.2.1'

    //db migrations
    implementation "org.flywaydb:flyway-core:5.1.4"

    //apache
    implementation 'commons-io:commons-io:2.6'

    //rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.1"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    //google
    implementation 'com.google.api-client:google-api-client:1.24.1'
    //purchases
    implementation 'com.google.apis:google-api-services-androidpublisher:v3-rev29-1.25.0'
    implementation 'com.google.api-client:google-api-client-jackson2:1.25.0'

    //firebase
    implementation 'com.google.firebase:firebase-admin:6.4.0'

    //vk
    implementation 'com.vk.api:sdk:0.5.12'

    //Retrofit
    implementation('com.squareup.retrofit2:retrofit:2.4.0') { exclude module: 'okhttp' }
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

noArg {
    annotation("ru.kuchanov.scpreaderapi.utils.NoArgConstructor")
}